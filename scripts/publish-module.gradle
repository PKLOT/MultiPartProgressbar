apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
}

def libraryVersion = '1.0.1'
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact androidSourcesJar
                groupId 'com.pklotcorp'
                artifactId 'multi-part-progressbar'
                version libraryVersion

                // ref: https://docs.gradle.org/current/userguide/publishing_maven.html#sec:modifying_the_generated_pom
                pom {
                    name = 'MultiPartProgressbar'
                    description = 'A progressbar which contains different parts of progress.'
                    url = 'https://github.com/PKLOT/MultiPartProgressbar'
                    licenses {
                        license {
                            name = 'APACHE LICENSE, VERSION 2.0'
                            url = 'https://github.com/PKLOT/MultiPartProgressbar'
                        }
                    }
                    developers {
                        developer {
                            id = 'pklotcorp'
                            name = 'PKLOT'
                            email = 'developers@pklotcorp.com'
                        }
                    }

                    // ref: https://maven.apache.org/pom.html#SCM
                    scm {
                        connection = 'scm:git:https://github.com/PKLOT/MultiPartProgressbar.git'
                        developerConnection = 'scm:git:https://github.com/PKLOT/MultiPartProgressbar.git'
                        url = 'https://github.com/PKLOT/MultiPartProgressbar.git'
                    }
                }
            }
        }
    }
}

// ref: https://docs.gradle.org/current/userguide/signing_plugin.html#sec:signatory_credentials
ext["signing.keyId"] = rootProject.ext["signing.keyId"]
ext["signing.password"] = rootProject.ext["signing.password"]
ext["signing.secretKeyRingFile"] = rootProject.ext["signing.secretKeyRingFile"]

signing {
    sign publishing.publications
}
